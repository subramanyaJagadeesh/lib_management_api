def diffLog = "$projectDir/src/main/resources/db/changelog/change-sets/db.changelog-${new Date().format('yyyyMMddHHmmss')}.yaml"
def localContext = "local"
configurations {
    liquibase
}

dependencies {
    liquibase 'org.liquibase.ext:liquibase-hibernate5:4.1.0'
}
/**
 * DB details are very specific to local profile as we always generate change logs on local
 * and then commit those changes so that they can be auto applied to subsequent environments.
 */
task liquibaseDiffChangelog(type: JavaExec) {
    group = "liquibase"

    classpath sourceSets.main.runtimeClasspath
    classpath configurations.liquibase
    main = "liquibase.integration.commandline.Main"

    args "--changeLogFile=${diffLog}"
    args "--referenceUrl=hibernate:spring:com.library_management.api?dialect=org.hibernate.dialect.MySQL8Dialect" +
            "&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy" +
            "&hibernate.implicit_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringImplicitNamingStrategy"
    args "--username=root"
    args "--password=root"
    args "--url=jdbc:mysql://localhost:3306/library_management?zeroDateTimeBehavior=convertToNull"
    args "--referenceDriver=liquibase.ext.hibernate.database.connection.HibernateDriver"
    args "--logLevel=flag"
    args "diffChangeLog"
}

/*
Use this to generate change log file with Data sql Inserts from Local DB
*/
task generateChangeLogWithDiffTypesData(type: JavaExec) {
    group = "liquibase"

    classpath sourceSets.main.runtimeClasspath
    classpath configurations.liquibase
    main = "liquibase.integration.commandline.Main"

    args "--changeLogFile=${diffLog}"
    args "--contexts=local"
    args "--url=jdbc:mysql://localhost:3306/learner_progress?zeroDateTimeBehavior=convertToNull"
    args "--username=root"
    args "--password=ueducation"
    args "--driver=com.mysql.cj.jdbc.Driver"
    args "--diffTypes=data"
    args "generateChangeLog"
}